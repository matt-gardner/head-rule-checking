% Replace null complementizers (0) with 0_NULL, to distinguish them from
% cardinal number zero.
/^0$/=zero >: /^-NONE-$/

relabel zero /^(.+)$/0_NULL/

% Pseudo-prepositions: whenever a verb is being used as a preposition, append
% an alternate POS (IN) to the label.
@PP <1 /^V(.(?!\|IN))*$/=verb

relabel verb /^(.+)$/$1|IN/

% When indexing words in the sentence, skip null elements beginning with *.
% NOTE: the "skip" suffix here must match SKIP_STR from index_words.py.
/^\*.*(?<!_SKIP)$/=null >: /^-NONE-$/

relabel null /^(.+)$/$1_SKIP/
